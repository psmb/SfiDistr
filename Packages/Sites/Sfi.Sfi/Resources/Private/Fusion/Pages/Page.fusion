#This is a default page object. It's being extended and never used directly.

prototype(Sfi.Sfi:Stylesheets) < prototype(Neos.Fusion:Array) {
	site = Neos.Fusion:Template {
		templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
		sectionName = 'stylesheets'
	}
	backend = Neos.Fusion:Template {
		templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
		sectionName = 'backendStylesheets'
		@process.1 = ${node.context.workspaceName != 'live' ? value:''}
	}
	@cache {
		mode = 'cached'
		entryIdentifier.a = 'a'
	}
}

prototype(Sfi.Sfi:Page) >
prototype(Sfi.Sfi:Page) < prototype(Neos.Neos:Page) {
	head {
		titleTag.content = ${String.stripTags(q(node).property('title')) + ' | ' + Translation.translate('sfi.abbrev', null, [], null, 'Sfi.Sfi')}

		norobots = afx`<meta name="robots" content="noindex, nofollow"/>`
		norobots.@if.notLive = ${node.context.workspaceName != 'live'}

		opengraph = Sfi.News:Opengraph
		opengraph.@if.isNews = ${q(node).is('[instanceof Sfi.News:AbstractArticle]')}

		opengraphImage = Sfi.Sfi:OpenGraphImage

		stylesheets = Sfi.Sfi:Stylesheets

		javascripts.headScripts = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
			sectionName = 'headScripts'
		}
		javascripts.@cache {
			mode = 'cached'
			entryIdentifier.a = 'a'
		}
	}
	htmlTag.attributes.lang = ${Array.first(node.context.dimensions.language)}
	bodyTag.attributes.class = 'Page'
	bodyTag.attributes.id = ${'page-' + q(node).property('_name')}
	body {
		templatePath = 'resource://Sfi.Sfi/Private/Templates/Pages/Page.html'
		partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
		layoutRootPath = 'resource://Sfi.Sfi/Private/Layouts'

		mainMenu = Neos.Neos:Menu {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/MainMenu.html'
			partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
			itemClass = 'LinkState H4 lineHeight-single'
			entryLevel = 1
			maximumLevels = 2
		}

		lang = ${Array.first(node.context.dimensions.language)}
		@context.altLang = ${this.lang == 'ru' ? 'en' : 'ru'}

		languageMenu = Sfi.Sfi:LanguageMenu
		@context.subMenuNode = ${q(site).find('#c030240e-7c32-40d8-aa84-586f1019966d').get(0)}
		subMenu = Neos.Neos:Menu {
			@if.isMain = ${q(node).is('[instanceof Sfi.Sfi:PageMain]') && subMenuNode ? true : false}
			templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/MainMenu.html'
			partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
			itemClass = 'ParagraphAlt lineHeight-single'
			startingPoint = ${subMenuNode}
			maximumLevels = 2
		}
		secondaryMenu = Neos.Neos:Menu {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/SecondaryMenu.html'
			itemCollection = ${q(site).children('links').children().get()}
		}
		linkToMain = Neos.Neos:NodeUri {
			node = ${site}
			@process.fix = ${String.replace(value, '/.html', '/')}
		}
		@context.sectionMenu = Neos.Neos:Menu {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/SectionMenu.html'
			entryLevel = 2
			maximumLevels = 0
		}
		@context.siblingsMenu = Neos.Neos:Menu {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/TypoScriptObjects/SectionMenu.html'
			itemCollection = ${q(documentNode).parent().children('[instanceof Neos.Neos:Document]').get()}
		}

		breadcrumbMenu = Neos.Neos:BreadcrumbMenu {
			attributes.class = 'BreadcrumbMenu hide-for-small-only'
			@if.isNotMain = ${q(node).is('[instanceof Sfi.Sfi:PageMain]') ? false : true}
			@if.isNotNews = ${q(node).is('[instanceof Sfi.News:AbstractNews]') ? false : true}
		}

		main = Neos.Neos:PrimaryContent {
			renderByType {
				condition = TRUE
				type = ${q(node).property('_nodeType.name')}
			}
			# @if.notSubscription = ${request.pluginArguments['newsletter']['@package'] != 'Psmb.Newsletter'}
		}
		# subscriptionPlugin = Psmb.Newsletter:SubscriptionPlugin

		javascripts.bodyScripts = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
			sectionName = 'bodyScripts'
		}
		javascripts.analytics = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Sfi/Private/Templates/SharedResources.html'
			partialRootPath = 'resource://Sfi.Sfi/Private/Partials'
			sectionName = 'analytics'
		}
		javascripts.@cache {
			mode = 'cached'
			entryIdentifier.a = 'a'
		}
	}
}

#Needed for listable-paginate widget to work
prototype(Neos.Neos:Page).@cache.entryIdentifier.pagination = ${request.pluginArguments.listable-paginate.currentPage}
prototype(Neos.Neos:Page).@cache.entryIdentifier.article = ${request.arguments.article}
root.@cache.entryIdentifier.pagination = ${request.pluginArguments.listable-paginate.currentPage}
root.@cache.entryIdentifier.article = ${request.arguments.article}

# Extend StyleguidePage with Sfi.Sfi:Page, to get correct CSS for Styleguide
prototype(Sfi.Styleguide:StyleguidePage) < prototype(Sfi.Sfi:Page) {
	# disable analytics
	body.javascripts.analytics >
}

root.fallbackPage {
	@position = 'before default'
	condition = TRUE
	type = 'Sfi.Sfi:Page'
}

# Disable stupid layout mechanism
root.layout>
