prototype(Sfi.Sfi:AlmanacLayout) < prototype(Neos.Fusion:Component) {
	@context.almanacRoot = ${q(site).find('[instanceof Sfi.Sfi:AlmanacRootPage]').get(0)}

	title = 'to-be-set'
	content = 'to-be-set'

	issuesNav = Sfi.Sfi:AlmanacIssuesMenu

	almanacBanner = Sfi.Site:AlmanacBanner {
		@context.node = ${q(site).find('[instanceof Sfi.Sfi:AlmanacIssuePage]').get(0)}
		@if.notCurrent = ${node != documentNode}
	}

	header = Sfi.Sfi:Component.AlmanacHeader

	footer = Sfi.Sfi:Footer

	nav = afx`
		<div id="almanac-rubrics" style="position: relative; top: -50px"></div>
		<div>
			<Sfi.Sfi:AlmanacResearchMenu/>
			<Sfi.Sfi:AlmanacRestMenu/>
		</div>
	`

	languageMenu = Sfi.Sfi:LanguageMenu {
		@process.lang>
		attributes.class = 'H5 marginTop-double'
		attributes.style = 'color: var(--color-primary); border: 2px solid var(--color-primary); padding: 2px 12px; display: inline-block;'
		content = ${altLang == 'en' ? 'In English' : 'По-русски'}
	}

	renderer = afx`
		<a class="SkipToContent" href="#maincontent">Перейти к основному содержимому</a>
		<div class="js-MobileNav">
			<div class="MobileNav-Content">
				<div class="js-stickInParent-parent">
					{props.header}
					<div class="row js-stickInParent-parent">
						<div class="small-12 large-6 large-push-3 columns">
							{props.linkToIssue}
							<div id="almanac-about" style="position: relative; top: -50px"></div>
							<h2 class="H2 marginBottom-double">{props.title}</h2>
							{props.content}
						</div>
						<div class="medium-6 large-3 large-pull-6 columns">
							<div class="js-stickInParent--large">
								{props.almanacBanner}
								<div class="marginTop-single marginBottom-double">
									{props.issuesNav}
								</div>

								<a
									@if.altLang={altLang == "en" ? true : false}
									class="Button marginVertical-single"
									target="_blank"
									href="https://www.pressa-rf.ru/cat/1/edition/e40540/"
								>
									Оформить подписку
								</a>
							</div>
						</div>
						<div class="medium-6 large-3 columns">
							<div class="js-stickInParent--large">
								<div class="marginTop-single">
									{props.nav}
								</div>
							</div>
						</div>
					</div>
				</div>
				<div class="marginTop-triple">
					{props.footer}
				</div>
			</div>
			<div class="MobileNav-Menu show-for-medium-down">
				<Sfi.Sfi:Component.SearchBox/>
				<Sfi.Sfi:AlmanacMainMenu mobileNav={true}/>
				{props.languageMenu}
				<div class="marginTop-single">
					<Sfi.Sfi:Component.DonateButton/>
				</div>
			</div>
		</div>
	`
}
