prototype(Sfi.Sfi:AlmanacEntry) < prototype(Neos.Neos:ContentComponent) {
	@context.lang = ${Array.first(node.context.dimensions.language)}
	@context.link = Neos.Neos:NodeUri {
		node = ${q(node).parent().parent().get(0)}
		additionalParams {
			article = ${node.name}
		}
	}
	@context.href = ${'asset://' + node.properties.asset.identifier}
	@context.href.@process.convertUris = Neos.Neos:ConvertUris
	@context.hrefEn = ${'asset://' + node.properties.assetEn.identifier}
	@context.hrefEn.@process.convertUris = Neos.Neos:ConvertUris
	issueStr = ${Translation.translate('almanac.issue', null, [], null, 'Sfi.Sfi')}
	issueNumber = ${this.issueStr + ' â„–' + q(node).parent().parent().property('issueNumber') + ','}
	issueNumber.@if.notIssuePage = ${!q(documentNode).is('[instanceof Sfi.Sfi:AlmanacIssuePage]')}

	author = ${node.properties.author}
	author2 = ${node.properties.author2}
	author3 = ${node.properties.author3}
	authorInfo = ${node.properties.authorInfo}
	authorInfo2 = ${node.properties.authorInfo2}
	authorInfo3 = ${node.properties.authorInfo3}
	source = ${node.properties.source}
	title = afx`<div class="Paragraph lineHeight-tight AlmanacEntry-title">{node.properties.title}</div>`
	title.@process.link = afx`<a class="Link" href={link}>{value}</a>`

	renderer = afx`
		<div class="marginVertical-double AlmanacEntry">
			<div>
				<div class="lineHeight-tight">
					<span class="H4 AlmanacEntry-author">{props.author}</span>
					<span class="ServiceEm AlmanacEntry-authorInfo" @if.live={node.context.workspace.name != 'live' || node.properties.authorInfo}>, {props.authorInfo}</span>
				</div>
				<div class="lineHeight-tight" @if.live={node.context.workspace.name != 'live' || node.properties.author2}>
					<span class="H4 AlmanacEntry-author">{props.author2}</span>
					<span class="ServiceEm AlmanacEntry-authorInfo" @if.live={node.context.workspace.name != 'live' || node.properties.authorInfo2}>, {props.authorInfo2}</span>
				</div>
				<div class="lineHeight-tight" @if.live={node.context.workspace.name != 'live' || node.properties.author3}>
					<span class="H4 AlmanacEntry-author">{props.author3}</span>
					<span class="ServiceEm AlmanacEntry-authorInfo" @if.live={node.context.workspace.name != 'live' || node.properties.authorInfo3}>, {props.authorInfo3}</span>
				</div>
				<div class="color-grayAlt">{props.title}</div>
			</div>
			<div class="ParagraphAlt--Small AlmanacEntry-source">
				{props.issueNumber} {props.source}
			</div>
		</div>
	`
}
