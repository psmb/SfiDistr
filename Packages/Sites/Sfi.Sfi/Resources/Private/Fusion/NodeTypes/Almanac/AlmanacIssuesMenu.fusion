prototype(Sfi.Sfi:AlmanacIssuesMenu) < prototype(Neos.Fusion:Collection) {
	@process.wrap = afx`
		<div id="almanac-issues" style="position: relative; top: -50px"></div>

		<div class="marginBottom-double" style="max-width: 280px">
			<h3 class="ServiceLight marginBottom-single">Поиск по номерам</h3>
			<form action="https://www.google.ru/cse" class="SearchBox" style="margin-right: 0; max-width: 100%" target="_blank">
				<div class="float-left" style="width:80%">
					<input name="cx" type="hidden" value="b278712f397153282"/>
					<input name="ie" type="hidden" value="UTF-8"/>
					<input name="q" placeholder="поиск" class="SearchBox-Field js-SearchBox-Field"/>
				</div>
				<div class="SearchBox-Submit icon-search float-left" style="width:20%">
					<input name="sa" type="submit" title="поиск"/>
				</div>
			</form>
		</div>

		<h3 class="ServiceLight marginBottom-single">{Translation.translate('almanac.allIssues', null, [], null, 'Sfi.Sfi')}</h3>
		<div class="marginTop-single">{value}</div>
	`
	collection = Neos.Fusion:RawCollection {
		collection = ${q(almanacRoot).children('[instanceof Sfi.Sfi:AlmanacIssuePage]').get()}
		itemName = 'node'
		itemRenderer = Neos.Fusion:RawArray {
			title = ${node.properties.issueNumber}
			year = ${String.pregMatch(node.properties.title, "/\(.*(\d\d\d\d)\)/i")[1]}
			year.@process.cutoff = ${value < 2014 ? 'до 2014' : value }
			link = Neos.Neos:NodeUri {
				node = ${node}
			}
			isActive = ${node == documentNode}
		}
		@process.group = ${Neos.Array.groupBy(value, 'year')}
	}
	itemName = 'group'
	itemKey = 'year'
	itemRenderer = Neos.Fusion:Collection {
		@process.wrap = afx`<ul style="display: flex; flex-wrap: wrap; max-width: 180px">{value}</ul>`
		@process.year = afx`
			<div style="display: flex; align-items: center;">
				<div style="width: 50px; font-weight: normal;" class="color-grayAlt marginRight-single lineHeight-single H4 marginBottom-single">{year}</div>
				{value}
			</div>
		`
		collection = ${group}
		itemName = 'item'
		itemRenderer = Neos.Fusion:Tag {
			@process.wrap = afx`<li style="
				width: 32px;
				height: 32px;
				border: 2px solid;
				line-height: 29px !important;
		" class={(item.isActive ? 'color-primary borderColor-primary' : 'color-grayAlt borderColor-gray') + ' textAlign-center marginRight-single lineHeight-single H4 marginBottom-single'}>{value}</li>`
			tagName = 'a'
			attributes.href = ${item.link}
			attributes.title = ${item.title}
			content = ${item.title}
		}
	}
}
