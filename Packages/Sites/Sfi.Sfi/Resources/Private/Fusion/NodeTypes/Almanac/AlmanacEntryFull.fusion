prototype(Sfi.Sfi:AlmanacEntryFull) < prototype(Neos.Neos:ContentComponent) {
	@context.lang = ${Array.first(node.context.dimensions.language)}
	@context.altLang = ${lang == 'ru' ? 'en' : 'ru'}
	@context.link = Neos.Neos:NodeUri {
		node = ${documentNode}
		additionalParams {
			article = ${node.name}
		}
	}
	@context.altLink = Neos.Neos:NodeUri {
		@context.altNode = ${q(documentNode).context({targetDimensions: {language: altLang}, dimensions: {language: [altLang]}})}
		@if.nodeExists = ${altNode.count() > 0}
		node = ${altNode.get(0)}
		additionalParams {
			article = ${node.name}
		}
	}
	@context.href = ${'asset://' + node.properties.asset.identifier}
	@context.href.@process.convertUris = Neos.Neos:ConvertUris
	@context.hrefEn = ${'asset://' + node.properties.assetEn.identifier}
	@context.hrefEn.@process.convertUris = Neos.Neos:ConvertUris
	keywordsStr = ${Translation.translate('keywords', null, [], null, 'Sfi.Sfi')}
	issueStr = ${Translation.translate('almanac.issue', null, [], null, 'Sfi.Sfi')}
	issueNumber = ${this.issueStr + ' №' + q(node).parent().parent().property('issueNumber') + ','}
	issueNumber.@if.notIssuePage = ${!q(documentNode).is('[instanceof Sfi.Sfi:AlmanacIssuePage]')}

	author = Neos.Neos:Editable {
		property = 'author'
		block = false
	}
	author2 = Neos.Neos:Editable {
		property = 'author2'
		block = false
	}
	author3 = Neos.Neos:Editable {
		property = 'author3'
		block = false
	}
	authorInfo = Neos.Neos:Editable {
		property = 'authorInfo'
		block = false
	}
	authorInfo2 = Neos.Neos:Editable {
		property = 'authorInfo2'
		block = false
	}
	authorInfo3 = Neos.Neos:Editable {
		property = 'authorInfo3'
		block = false
	}
	source = Neos.Neos:Editable {
		property = 'source'
		block = false
	}
	linkAlt = afx`<div class="marginTop-half"><a class="Link" href={altLink}>
		<div @if.onlyRussian={lang == 'ru' && node.properties.assetEn && node.properties.asset}>Эта статья также доступна на английском языке</div>
		<div @if.alsoRussian={lang == 'ru' && node.properties.assetEn && !node.properties.asset}>Эта статья доступна только на английском языке</div>
		<div @if.alsoRussian={lang == 'en' && node.properties.asset && node.properties.assetEn}>This article is also available in Russian</div>
		<div @if.onlyRussian={lang == 'en' && node.properties.asset && !node.properties.assetEn}>This article is only available in Russian</div>
	</a></div>`
	linkAlt.@if.live = ${((lang == 'ru' && node.properties.assetEn) || (lang == 'en' && node.properties.asset))}

	download = afx`<div class="marginTop-half"><a class="Button" href={lang == 'en' ? hrefEn : href}>{lang == 'en' ? 'Download' : 'Скачать'}</a></div>`

	renderer = afx`
		<div class="marginVertical-double AlmanacEntry">
			<div>
				<div class="lineHeight-tight">
					<span class="H4 AlmanacEntry-author">{props.author}</span>
					<span class="ServiceEm AlmanacEntry-authorInfo" @if.live={node.context.workspace.name != 'live' || node.properties.authorInfo}>, {props.authorInfo}</span>
				</div>
				<div class="lineHeight-tight" @if.live={node.context.workspace.name != 'live' || node.properties.author2}>
					<span class="H4 AlmanacEntry-author">{props.author2}</span>
					<span class="ServiceEm AlmanacEntry-authorInfo" @if.live={node.context.workspace.name != 'live' || node.properties.authorInfo2}>, {props.authorInfo2}</span>
				</div>
				<div class="lineHeight-tight" @if.live={node.context.workspace.name != 'live' || node.properties.author3}>
					<span class="H4 AlmanacEntry-author">{props.author3}</span>
					<span class="ServiceEm AlmanacEntry-authorInfo" @if.live={node.context.workspace.name != 'live' || node.properties.authorInfo3}>, {props.authorInfo3}</span>
				</div>
			</div>
			<div class="ParagraphAlt--Small AlmanacEntry-source">
				{props.issueNumber} {props.source}
			</div>
			<div @if.live={node.context.workspace.name != 'live' || node.properties.DOI} class="ParagraphAlt--Small AlmanacEntry-annotation">DOI: <Neos.Neos:Editable property="DOI" block={false}/></div>
			<div class="ParagraphAlt marginVertical-half AlmanacEntry-annotation">
				<Neos.Neos:Editable property="annotation" block={false}/>
			</div>
			<div @if.live={node.context.workspace.name != 'live' || node.properties.keywords} class="ParagraphAlt AlmanacEntry-keywords">
				{props.keywordsStr}: <Neos.Neos:Editable property="keywords" block={false}/>
			</div>
			{props.linkAlt}
			<br/>
			{props.download}
		</div>
	`
}
